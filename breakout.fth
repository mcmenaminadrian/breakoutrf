LOADMODULE ./modules/ncurses/ncurses.so DROP
VARIABLE DELAY
VARIABLE BLOCKCOUNT
VARIABLE SCORE
VARIABLE LEVEL
VARIABLE XBAT
VARIABLE BALLXYV 2 CELLS ALLOT  \ x, y, direction
DECIMAL 40 CONSTANT DEEP 
DECIMAL 140 CONSTANT WIDTH
DECIMAL 66 CONSTANT WALL
DECIMAL 5 CONSTANT BALLDELAYLIMIT
VARIABLE CURRENTBALLDELAY
VARIABLE OLDBALLXY 1 CELLS ALLOT

: ERRORDETECT     \ Generic error trap
 ( n -- )
 -1 = IF
  ." ERROR DETECTED - FAILING" CR
  QUIT
 THEN
;

: CREATEPAIRS     \ Create colour pairs
 ( -- )
 DECIMAL
 1 5 0 INIT_PAIR ERRORDETECT    \ basic red
 2 17 0 INIT_PAIR ERRORDETECT   \ very dark blue
 3 18 0 INIT_PAIR ERRORDETECT   \ dark blue
 4 19 0 INIT_PAIR ERRORDETECT   \ slightly less dark dlue
 5 20 0 INIT_PAIR ERRORDETECT   \ blue
 20 0 20 INIT_PAIR ERRORDETECT  \ reversed blue
 6 21 0 INIT_PAIR ERRORDETECT   \ greeish blue
 7 39 0 INIT_PAIR ERRORDETECT   \ turquoise
 21 0 51 INIT_PAIR ERRORDETECT  \ resversed turquoise
 8 38 0 INIT_PAIR ERRORDETECT   \ bit greener
 9 37 0 INIT_PAIR ERRORDETECT   \ greener still
 10 36 0 INIT_PAIR ERRORDETECT  \ bluish green
 11 35 0 INIT_PAIR ERRORDETECT  \ more green
 12 84 0 INIT_PAIR ERRORDETECT  \ light green
 13 83 0 INIT_PAIR ERRORDETECT  \ bright green
 14 82 0 INIT_PAIR ERRORDETECT  \ basic green
 22 0 46 INIT_PAIR ERRORDETECT \ reversed green
 15 202 0 INIT_PAIR ERRORDETECT \ yellow-mauve
 16 172 0 INIT_PAIR ERRORDETECT \ organe
 17 196 0 INIT_PAIR ERRORDETECT \ pinkish
 18 124 0 INIT_PAIR ERRORDETECT \ purplish
 19 52 0 INIT_PAIR ERRORDETECT  \ deep purple
 23 0 11 INIT_PAIR ERRORDETECT \ reverse yellow
 24 0 13 INIT_PAIR ERRORDETECT \ reverse purple
 25 0 10 INIT_PAIR ERRORDETECT \ reverse green
 26 0 15 INIT_PAIR ERRORDETECT \ reverse white
 WALL 0 124 INIT_PAIR ERRORDETECT \ red wall colour
;


: DRAWBOUNDARY    \ draw a boundary wall
 ( -- )
 WALL COLOR_PAIR ATTRON
 DEEP 0 DO
   DEEP I - 0  [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
   DEEP I - WIDTH [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
 LOOP
 WIDTH  0 DO
   0 WIDTH I - [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
 LOOP
 WALL COLOR_PAIR ATTROFF
;

: DRAWBLOCKS      \ draw blocks
  ( -- )
  [ DECIMAL 20 ] LITERAL COLOR_PAIR ATTRON
  WIDTH 1 DO
    7 I [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
    1 BLOCKCOUNT +!
  LOOP
  [ DECIMAL 20 ] LITERAL COLOR_PAIR ATTROFF

  [ DECIMAL 21 ] LITERAL COLOR_PAIR ATTRON
  WIDTH 1 DO
    8 I [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
    BLOCKCOUNT @ 1+ BLOCKCOUNT !
  LOOP
  [ DECIMAL 21 ] LITERAL COLOR_PAIR ATTROFF

  [ DECIMAL 22 ] LITERAL COLOR_PAIR ATTRON
  WIDTH 1 DO
    9 I [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
    1 BLOCKCOUNT +!
  LOOP
  [ DECIMAL 22 ] LITERAL COLOR_PAIR ATTROFF

  [ DECIMAL 23 ] LITERAL COLOR_PAIR ATTRON
  WIDTH 1 DO
    14 I [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
    1 BLOCKCOUNT +!
  LOOP
  [ DECIMAL 23 ] LITERAL COLOR_PAIR ATTROFF

  [ DECIMAL 24 ] LITERAL COLOR_PAIR ATTRON
  WIDTH 1 DO
    15 I [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
    1 BLOCKCOUNT +!
  LOOP
  [ DECIMAL 24 ] LITERAL COLOR_PAIR ATTROFF

  [ DECIMAL 25 ] LITERAL COLOR_PAIR ATTRON
  WIDTH 1 DO
    16 I [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
    1 BLOCKCOUNT +!
  LOOP
  [ DECIMAL 25 ] LITERAL COLOR_PAIR ATTROFF

  1 LITERAL COLOR_PAIR ATTRON
  WIDTH 1 DO
    19 I [ DECIMAL 32 ] LITERAL A_REVERSE OR MVADDCH ERRORDETECT
    1 BLOCKCOUNT +!
  3 +LOOP
  1 COLOR_PAIR ATTROFF

;

: DRAWBAT
  ( -- )
  [ DECIMAL 23 ] LITERAL COLOR_PAIR ATTRON
  DEEP 1- XBAT @ [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
  [ DECIMAL 32 ] LITERAL ADDCH ERRORDETECT
  [ DECIMAL 23 ] LITERAL COLOR_PAIR ATTROFF
;

: ERASEBAT
  ( -- )
  0 COLOR_PAIR ATTRON
  DEEP 1- XBAT @ [ DECIMAL 32 ] LITERAL MVADDCH ERRORDETECT
  [ DECIMAL 32 ] LITERAL ADDCH ERRORDETECT
  0 COLOR_PAIR ATTROFF
;
 
: ERASEBALL
  ( -- )
  0 COLOR_PAIR ATTRON
  OLDBALLXY 1 CELLS + @ OLDBALLXY @ [ HEX 20 ] LITERAL MVADDCH ERRORDETECT
  0 COLOR_PAIR ATTROFF
;

: DRAWBALL
  ( -- )
  ERASEBALL
  [ DECIMAL 26 ] LITERAL COLOR_PAIR ATTRON
  BALLXYV 1 CELLS + @ BALLXYV @ [ HEX 20 ] LITERAL MVADDCH ERRORDETECT
  [ DECIMAL 26 ] LITERAL COLOR_PAIR ATTROFF
  REFRESH ERRORDETECT
;

: STARTGAME
  ( -- )
 \ give ball initial position and direction
 \ fixed for now
 1 BALLXYV !
 [ DECIMAL 20 ] LITERAL BALLXYV 1 CELLS + !
 1 BALLXYV 2 CELLS + !
 0 CURRENTBALLDELAY !
 DRAWBALL
;

: PROCESSKEYBAT
  ( c -- )
  DUP 
  [ HEX 78 ] LITERAL = WIDTH XBAT @ - 2 > AND
  IF
    ERASEBAT
    1 XBAT +!
    DRAWBAT
    REFRESH ERRORDETECT
    DROP
    ELSE [ HEX 7A ] LITERAL = XBAT @ 1 > AND
    IF
      ERASEBAT
      -1 XBAT +!
      DRAWBAT
      REFRESH ERRORDETECT
    THEN
  THEN
;

: MOVEBALL
  ( -- )
  BALLXYV @ OLDBALLXY !
  BALLXYV CELL+ @ OLDBALLXY CELL+ !
  BALLXYV 2 CELLS + @
  CASE
    0 OF -1 BALLXYV +! -1 BALLXYV CELL+ +! ENDOF
    1 OF 1 BALLXYV +! -1 BALLXYV CELL+ +! ENDOF
    2 OF 1 BALLXYV +! 1 BALLXYV CELL+ +! ENDOF
    3 OF -1 BALLXYV +! 1 BALLXYV CELL+ +! ENDOF
  ENDCASE
;

: SIDEBALL
  ( -- )
  BALLXYV 2 CELLS + @
  CASE
    0 OF 1 BALLXYV 2 CELLS + ! ENDOF
    1 OF 0 BALLXYV 2 CELLS + ! ENDOF
    2 OF 3 BALLXYV 2 CELLS + ! ENDOF
    3 OF 2 BALLXYV 2 CELLS + ! ENDOF
  ENDCASE
;

: TOPBALL
  ( -- )
  BALLXYV 2 CELLS + @
  CASE
    0 OF 3 BALLXYV 2 CELLS + ! ENDOF
    1 OF 2 BALLXYV 2 CELLS + ! ENDOF
    2 OF 1 BALLXYV 2 CELLS + ! ENDOF
    3 OF 0 BALLXYV 2 CELLS + ! ENDOF
  ENDCASE
;


: TESTBALL
  ( -- )
  BALLXYV @ DUP
  0= SWAP WIDTH = OR
  IF \ hit sides
    ERASEBALL
    SIDEBALL
    MOVEBALL
  ELSE
    BALLXYV CELL+ @
    0=
    IF \ hit top
      ERASEBALL
      TOPBALL
      MOVEBALL
    ELSE 
      BALLXYV CELL+ @ BALLXYV @ MVINCH
      A_COLOR AND
      IF
        BALLXYV CELL+ @ 
        DEEP 1- =
        IF
          ERASEBALL
          TOPBALL
          MOVEBALL
        ELSE
          DRAWBALL
          TOPBALL
        THEN
      ELSE
        DRAWBALL
      THEN
    THEN
  THEN
;

: PROCESSBALL
  ( -- )
  1 CURRENTBALLDELAY +!
  CURRENTBALLDELAY @ BALLDELAYLIMIT >=
  IF
    0 CURRENTBALLDELAY !
    MOVEBALL
    TESTBALL
  THEN
;  


: DISPLAYSET
  ( -- )
  NODELAYONSTD ERRORDETECT
  NOECHO ERRORDETECT
;

: DISPLAYRESET
  ( -- )
  ECHO ERRORDETECT
  NODELAYOFFSTD ERRORDETECT
;

: CHECKOUTOFBOUNDS
  ( -- b)
  BALLXYV CELL+ @
  DEEP =
;


: GAME
  ( -- )
 STARTGAME
 BEGIN
   DISPLAYSET
   GETCH DUP
   DISPLAYRESET
   10 MS
   -1 = IF DROP ELSE 
     DUP
     [ HEX 51 ] LITERAL =
     IF
       LEAVE 
     THEN     \ capital Q to quit
     PROCESSKEYBAT
   THEN
   10 MS
   PROCESSBALL
   CHECKOUTOFBOUNDS
   IF
     LEAVE
   THEN
 AGAIN
;


: BREAKOUT
 ( -- )
 DECIMAL
 1 LEVEL !
 0 SCORE !
 0 BLOCKCOUNT !

 INITSCR CLEAR
 CURS_INVIS
 START_COLOR ERRORDETECT
 CREATEPAIRS
 DRAWBOUNDARY
 DRAWBLOCKS
 16 XBAT !
 DRAWBAT
 GAME
 GETCH ERRORDETECT
 CURS_NORMAL
 ENDWIN
 
;
